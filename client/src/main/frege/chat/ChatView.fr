
module chat.ChatView where

import fregefx.JavaFxType
import fregefx.JavaFxAll hiding (ListView)
import fregefx.JavaFxUtils

import Data.JSON
import Control.Concurrent

import chat.Remote (doPost)
import chat.Data (Post)

totalWidth   = 600.0
totalHeight  = 500.0

type InputRef = JFXRef (TextField, TextArea) 

main args = do        
    ready <- MVar.newEmpty     -- for thread coordination, set in showUI thread, start autorefresh when ready
    async do                   -- JavaFX would block the main thread otherwise after launching the UI thread
        FregeFX.launch $ withStage $ buildUI ready 
    chatRef <- ready.take      -- proceeding in the main thread, wait until view is available
    autorefresh chatRef        -- start the autorefresh
    
autorefresh :: JFXRef TextArea -> IO ()
autorefresh chatRef = do
    inIO (refresh chatRef) $ \_ -> do
        Thread.sleep 500
        autorefresh chatRef
	                                
refresh :: JFXRef TextArea -> JFX ()
refresh chatRef = do
    chatArea <- chatRef.get
    getPostings `thenDo` (onChatLoaded chatArea)
    
getPostings âˆ· IO (Maybe String)
getPostings = doPost "http://localhost:8080/json/list" ""

buildUI :: MVar (JFXRef TextArea) -> Group -> Stage -> JFX Group
buildUI ready root stage = do
    stage.setTitle "Frege Chat"
    stage.setWidth  totalWidth
    stage.setHeight totalHeight
    
    senderField <- TextField.new "Dierk"                    :: JFX TextField
    messageArea <- TextArea.new  "My message: Frege rocks!" :: JFX TextArea
    chatArea    <- TextArea.new  "empty"                    :: JFX TextArea
    
    chatRef  <- JFXRef.new chatArea
    inputRef <- JFXRef.new (senderField, messageArea)
    
    pane <- Pane.new () :: JFX Pane
    root <: do
        pane `addNode` do
            r <- Rectangle.new 0.0 0.0 totalWidth totalHeight
            r.setStyle "-fx-fill:radial-gradient(center 25% 25%, radius 60%, reflect, orange, red );"
            return r
        pane <: do
            vbox <- VBox.new 5d :: JFX VBox
            vbox.setPadding =<< insets 25
            vbox <: return senderField
            vbox <: return messageArea           	
            vbox <: do
                sendButton <- Button.new "Send"     
                actionIO_ sendButton (submitPost inputRef)
            vbox <: return chatArea
    scene <- stage.getScene
    (ready.put chatRef) `thenDo` noAction
    return root

noAction _ = return ()

submitPost :: InputRef -> IO ()
submitPost inputRef = 
    inIO (makePost inputRef) $ \payload -> do
        Thread.current() >>= _.getName >>= println
        doPost "http://localhost:8080/json/create" payload 
        return ()

makePost :: InputRef -> JFX String
makePost inputRef = do
    (sender, message) <- inputRef.get
    senderText  <- sender.getText
    messageText <- message.getText
    return $ show Post {sender = senderText, message = messageText}.toJSON

onChatLoaded :: TextArea -> (Maybe String) -> JFX ()
onChatLoaded chatArea result = do
    case (result >>= parseJSON) of
        Nothing                -> return ()
        Just (posts :: [Post]) -> do
            output = unlines $ map (\post -> post.sender ++ ":\n    " ++ post.message ++ "\n") posts	
            chatArea.setText output
    

